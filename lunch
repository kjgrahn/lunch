#!/usr/bin/perl -w
#
# Copyright (c) 2002, 2012 Jörgen Grahn
# All rights reserved.
#
# lunch - calculate working hours from
# (in, out, lunch) triples
#
# Bugs: the original Excel sheet does ceil() on
# tenths of hours - this one rounds.
# I lose time, but better safe than sorry...
#
# /jgr
#
use strict;

while(<>) {

    /^\s*(\d{1,2}.\d\d)\s+(\d{1,2}.\d\d)\s+(\d{1,2}.\d\d)/ or print, next;

    my ($morn, $eve, $lunch) = ($1, $2, $3);

    # make minutes of everything

    my ($mh, $mm) = $morn =~ /(\d+).(\d+)/;
    my ($eh, $em) = $eve =~ /(\d+).(\d+)/;
    my ($lh, $lm) = $lunch =~ /(\d+).(\d+)/;

    die "most hours only have sixty minutes...\n"
        unless ($mm<60 && $em<60 && $lm<60);

    my $ms = $mh*60+$mm;
    my $es = $eh*60+$em;
    my $ls = $lh*60+$lm;

    # calculate working time, in hours

    my $works = $es - $ms - $ls;

    my $workh = $works / 60;

    # finally, our new time reporting system wants me
    # to calculate the deviation from standard time
    # (normally 8.00) manually

    my $deviation = $workh - 8;

    printf("%5s %5s %4s ->%2dh%02dm (%5.2f h; %4.1f h) [%+.2f]\n",
           $morn, $eve, $lunch,
           $works/60, $works % 60,
           $workh, $workh,
	   $deviation);
}

exit 0;
